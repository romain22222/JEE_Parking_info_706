plugins {
    id 'war'
    id 'eclipse'
    id 'idea'
}

repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()
}

// java version: source and bytecode 
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    //providedCompile group: 'javax', name: 'javaee-web-api', version: '8.0'
    //providedRuntime group: 'org.glassfish.web', name: 'javax.servlet.jsp.jstl', version: '1.2.5'
    providedCompile group: 'jakarta.platform', name: 'jakarta.jakartaee-web-api', version: '8.0.0'
    providedRuntime group: 'org.glassfish.web', name: 'jakarta.servlet.jsp.jstl', version: '1.2.6'
}

// encodage des fichIers en UTF8
compileJava.options.encoding = 'UTF-8'

// configuration encodage pour eclipse
eclipseJdt.doLast {
    File f = file('.settings/org.eclipse.core.resources.prefs')
    f.write('eclipse.preferences.version=1\n')
    f.append('encoding/<project>=UTF-8')
}
// configuration encodage pour IntelliJ
idea {
  project {
    ipr {
      withXml { xmlProvider ->
        def encoding = xmlProvider.asNode().component.find { it.@name == "Encoding" }
        encoding.appendNode("file", [url: "PROJECT", charset: "UTF-8"])
      }
    }
  }
}

// configuration JEE pour eclipse
eclipse.wtp.facet { 
    facet name: 'jst.web', version: '4.0'
    facet name: 'jpt.jpa', version: '2.1' 
}
